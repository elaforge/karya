#!/bin/zsh

prefixes=("$@")

flags=(
    # time profiling
    -sRunProfile.gc # emit runtime summary
    -p # emit .prof file

    # heap profiling
    -L42 # field length for cost center names in heap profile
)

# Just documentation for now.
all_heap_flags=(
    hc # by producing cost-center stack
    hm # by module
    hd # by closure description
    hy # by type
    hr # by retainer set
    hb # by biography
)

# Take flags from $heap and split on spaces.
# This way you can pass 'hc hbdrag', (-hc -hbdrag) will be passed, and the
# output will be named hc_hbdrag.*
heap_flags=(${=${heap:-hm}})

if [[ $prefixes = seq ]]; then
    run_profile=build/profile/seq
    profiles=seq
else
    run_profile=build/profile/RunProfile
    profiles=($($run_profile --list $prefixes))
fi
out=prof

function run() {
    echo "$@"
    "$@"
}

for prof in $profiles; do
    dir=$out/$(date +%y-%m-%d)/${prof/*-/}
    mkdir -p $dir
    stem=$dir/$heap_flags
    # Due to shells being insane, this substitution can't happen to heap_flags
    # directly.
    stem=${stem/ /_}
    if [[ $prof = seq ]]; then
        cores=1 run $run_profile +RTS $flags -${^heap_flags} -RTS
    else
        run $run_profile +RTS $flags -${^heap_flags} -RTS "^$prof$"
    fi

    basename=$(basename $run_profile)
    for suf in gc prof hp aux tix; do
        if [[ -e $basename.$suf ]]; then
            mv $basename.$suf $stem.$suf
        fi
    done
    if [[ -e $stem.hp ]]; then
        echo $stem.pdf
        hp2ps -c <$stem.hp | ps2pdf - - >$stem.pdf
    fi
done
