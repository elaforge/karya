#!/usr/bin/env zsh
# Copyright 2013 Evan Laforge
# This program is distributed under the terms of the GNU General Public
# License 3.0, see COPYING or http://www.gnu.org/licenses/gpl-3.0.txt

# Run tests given in prefixes.  If some of them require init, run each in a
# separate process.
# Redirect stderr to test.output.
# Grep for failures.

# TODO: display warn and error log msgs

set -euo pipefail

# If set, then exclude the test modules from the hpc results.
hpc_exclude_test=${hpc_exclude_test-} # Add 't' to default true.

while [[ $1 = */* ]]; do
    binaries+=($1)
    shift
done

# Multiple tests will aggregate the runs.  Ultimately this is because I
# want to run both RunTests compiled with test flags and with profiling flags.
if [[ ${#binaries} -eq 0 ]]; then
    echo "usage: $0 build/test/RunTests [ build/profile/Run ... ] [ prefix ...]"
    exit 1
fi

prefixes=("$@")
if [[ -z $prefixes ]]; then
    prefixes=('^normal-')
fi

# Write test stdout to here.
out_dir=build/test/output
all_tix=$out_dir/all.tix

function run {
    echo "$@"
    "$@"
}

# The subprocesses will set HPCTIXFILE appropriately.
# Apparently there's no way to turn off hpc, and it doesn't like /dev/null.
for bin in $binaries; do
    HPCTIXFILE=build/test/tmp/tix.tix \
        run $bin --jobs=auto --clear-dirs --output=$out_dir $prefixes
done
rm -f build/test/tmp/tix.tix

# Do one test run to check all the outputs.
# Explicitly match no tests, because I just want to check them.
run $binaries[1] --jobs=auto --output=$out_dir --check-output '^$'
failures=$?
rm -f *.tix(N) # these things are really annoying

# Optionally disable HPC.
if [[ -z ${skip_hpc:+t} ]]; then
    # hpc is super slow
    echo 'Generating hpc markup in the background...'
    (
        exec >$out_dir/hpc.log 2>&1
        if ! [[ -e $all_tix ]]; then
            run hpc sum --union --output=$all_tix $out_dir/*.tix
        fi
        rm -rf build/hpc
        mkdir -p build/hpc
        if [[ -n $hpc_exclude_test ]]; then
            nontest=$(tools/all_hs.py notest dotted)
            nontest=--include=${^nontest}
        else
            nontest=
        fi
        hpc markup --destdir=build/hpc $nontest $all_tix
        # I'm more used to typing index.html
        (cd build/hpc && ln -fs hpc_index.html index.html)
        echo Done generating hpc.
        rm -rf .hpc
    ) &
fi

exit $failures
