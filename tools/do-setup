#!/bin/zsh
# Called by tools/setup-* to initialize a new install.
#
# All the file operations in here should be skipped for files that already
# exist, to avoid overwriting local config.

set -x # trace cmds

data=${data:?data not set}
hs_dir=${hs_dir:?hs_dir not set}
hs_package=${hs_package:?hs_package not set}


data_dirs=(im inst_db prof save)
local_hs=(Config.hs Instrument.hs KeyLayout.hs Repl.hs ShakeConfig.hs)
non_hs=(ky)

mkdir -p log

for dir in $data_dirs; do
    mkdir -p $data/$dir
    if [[ ! -e $dir ]]; then
        echo ln -s $data/$dir
        ln -s $data/$dir
    fi
done
if [[ ! -e save/default ]]; then
    cp data/default.save save/default
fi

# Make a re-export module for each config hs.
for hs in $local_hs; do
    if [[ -e Local/$hs ]]; then
        continue
    fi
    module=${hs%.hs}

    echo create Local/$hs
    cat >Local/$hs <<EOF
-- Generated by $0
module Local.$module (module $hs_package.$module) where
import $hs_package.$module
EOF

done

for fn in $non_hs; do
    if [[ ! -e Local/$fn ]]; then
        ln -s ../$hs_dir/$fn Local/$fn
    fi
done
