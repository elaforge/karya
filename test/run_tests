#!/bin/zsh
# Copyright 2013 Evan Laforge
# This program is distributed under the terms of the GNU General Public
# License 3.0, see COPYING or http://www.gnu.org/licenses/gpl-3.0.txt

# Run tests given in prefixes.  If some of them require init, run each in a
# separate process.
# Redirect stderr to test.output.
# Grep for failures.

# TODO: display warn and error log msgs

run_tests=$1
shift
if [[ -z $run_tests ]]; then
    echo "usage: $0 run/tests/binary [ prefixes ...]"
    exit 1
fi

# Run tests in parallel.
if which parallel >/dev/null; then
    parallel=t
else
    echo parallel command not found, running tests in serial.
fi

# This is appended to.  It should be reset along with the coverage data every
# time test_obj/RunTests is built.
test_output=build/test.output

prefixes=("$@")
if [[ -z $prefixes ]]; then
    prefixes=('^normal-')
fi

function run {
    "$@"
    local r=$?
    if [[ $r -ne 0 ]]; then
        echo '__->' cmd returned $r: "$@"
    fi
}

# Some tests are more expensive than others.  Scramble them around to try
# to distribute them evenly.
function scramble {
    python -c 'import sys, random
x=list(sys.stdin); random.shuffle(x); sys.stdout.writelines(x)'
}

# Tests will recreate this if they need to.
rm -rf build/test/tmp

# I capture stderr as well because Debug.Trace.trace prints to stderr,
# and apparently there's no way to change that.
time (
if [[ -n $parallel ]]; then
    $run_tests --list $prefixes | scramble \
        | parallel -j$(tools/ncpus) --max-args=50 -m $run_tests \
            --noninteractive '{}' >$test_output 2>&1
else
    run $run_tests --noninteractive $prefixes >$test_output 2>&1
fi
)

echo -n '\nTests complete.  Failures:\n'
test/extract_failures.py <$test_output
passes=$(grep '^++->' $test_output | wc -l)
failures=$(grep '^__->' $test_output | wc -l)
tests=$(grep '^-----' $test_output | wc -l)
echo $failures failed / $passes passed / $tests tests

echo 'Generating hpc markup in the background...'
(
    mkdir -p build/hpc
    hpc markup --destdir=build/hpc RunTests | grep -v Writing
    # I'm more used to typing index.html
    (cd build/hpc && ln -fs hpc_index.html index.html)
    echo Done generating hpc.
) &

exit $failures
