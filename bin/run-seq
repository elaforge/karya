#!/usr/bin/env zsh
# Run seq.  First make sure it's built and logview is running.

set -eu

# Keep this binaries up to date.  This isn't appropriate if you don't have
# im, so in that case maybe I'll need some way of getting the config.
binaries=(
    logview faust-im sampler-im seq
)
if ! bin/mk build/$mode/${^binaries}; then
    echo $0: build failed
    exit 1
fi

# Keep the faust preview cache up to date.  auto_preview= to turn it off.
if [[ -n ${auto_preview-t} ]]; then
    echo build/opt/faust-im render-preview
    build/opt/faust-im render-preview
fi

# TODO This is hardcoded for my system, but should be in some local config.
if [[ $OSTYPE = linux-gnu ]]; then
    geometry='--geometry=628x300+1290+900'
else
    geometry=
fi

mkdir -p log

case $(uname) in
Darwin)
    # Get rid of OS X window restore state.  It doesn't actually work, but it
    # makes an annoying "restore state?" dialog on startup after a crash.
    dir=~/Library/Saved\ Application\ State
    rm -rf $dir/elaforge.seq.{seq,logview,browser}.savedState
    ;;
[Ll]inux)
    # FLTK is supposed to detect this, but probably I don't have DPI set up on
    # X.
    export FLTK_SCALING_FACTOR=2
    ;;
esac

build/$mode/logview $geometry &

if [[ -e seq-repl ]]; then
    echo "$0: seq-repl exists, is a copy already running?"
    exit 1
fi
build/$mode/seq "$@"
if [[ $? -ne 0 ]]; then
    rm seq-repl
fi

